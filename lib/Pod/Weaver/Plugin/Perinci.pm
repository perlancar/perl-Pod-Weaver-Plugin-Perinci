package Pod::Weaver::Plugin::Perinci;

use 5.010;
use Moose;
with 'Pod::Weaver::Role::Section';

use Log::Any '$log';

use Perinci::To::POD;
use Pod::Elemental;
use Pod::Elemental::Element::Nested;

# VERSION

sub weave_section {
    $log->trace("-> ".__PACKAGE__."::weave_section()");
    my ($self, $document, $input) = @_;

    my $filename = $input->{filename} || 'file';

    # guess package name from filename
    my $package;
    if ($filename =~ m!^lib/(.+)\.pm$!) {
        $package = $1;
        $package =~ s!/!::!g;
    } else {
        #$self->log(["skipped file %s (not a Perl module)", $filename]);
        $log->debugf("skipped file %s (not a Perl module)", $filename);
        return;
    }

    unshift @INC, "lib" unless 'lib' =~ @INC;

    # generate the POD and insert it to FUNCTIONS section
    my $url = $package; $url =~ s!::!/!g; $url .= "/";
    my $doc = Perinci::To::POD->new(url => $url);
    $doc->delete_doc_section('summary'); # already handled by other plugins
    $doc->delete_doc_section('version'); # ditto
    my $pod_text = $doc->generate_doc;

    my $found;
    while ($pod_text =~ /^=head1 ([^\n]+)\n(.+?)(?=^=head1|\z)/msg) {
        $found++;
        my $fpara = Pod::Elemental::Element::Nested->new({
            command  => 'head1',
            content  => $1,
            children => Pod::Elemental->read_string($2)->children,
        });
        push @{ $input->{pod_document}->children }, $fpara;
    }
    if ($found) {
        $self->log(["adding spec POD for %s", $filename]);
        $log->infof("adding spec POD for %s", $filename);
    }
    $log->trace("<- ".__PACKAGE__."::weave_section()");
}

1;
# ABSTRACT: Insert POD from Rinci metadata

=for Pod::Coverage weave_section

=head1 SYNOPSIS

In your C<weaver.ini>:

 [-Perinci]


=head1 DESCRIPTION

This plugin inserts POD documentation (generated by L<Perinci::To::POD>).


=head1 TODO

If document already has =head1 FUNCTIONS, replace it instead of adding another
one. Same goes for =head1 DESCRIPTION (and later =head1 ATTRIBUTES, =head1
METHODS, =head1 VARIABLES).


=head1 SEE ALSO

L<Perinci::To::Pod>

L<Pod::Weaver>
